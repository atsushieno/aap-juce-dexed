plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

kotlin {
    jvmToolchain(11)
}

android {
    namespace "org.androidaudioplugin.ports.juce.dexed"

    compileSdkVersion 33

    defaultConfig {
        applicationId "org.androidaudioplugin.ports.juce.dexed"
        minSdkVersion    29
        targetSdkVersion 33
        externalNativeBuild {
            cmake {
                arguments "-DAAP_DIR=${System.env.AAP_DIR != null ? System.env.AAP_DIR : "$projectDir/../external/aap-core"}",
                        "-DAAP_JUCE_DIR=${System.env.AAP_JUCE_DIR != null ? System.env.AAP_JUCE_DIR : "$projectDir/../external/aap-juce"}",
                        "-DANDROID_STL=c++_shared", "-DANDROID_CPP_FEATURES=exceptions rtti", "-DANDROID_ARM_NEON=ON"
            }
        }
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    sourceSets {
        main.java.srcDirs +=
            ["../external/dexed/libs/JUCE/modules/juce_gui_basics/native/javaopt/app",
             "../external/aap-juce/java"]
        main.res.srcDirs +=
            []
    }

    dependencies {
        implementation libs.androidx.core.ktx
        implementation libs.startup.runtime
        implementation libs.aap.core
        implementation libs.aap.midi.device.service
        implementation libs.aap.ui.web
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    buildFeatures {
        prefab true
    }

    ndkVersion libs.versions.ndk.get()

    // FIXME: remove this once we upgraded to Gradle 8.1 (AGP issue)
    compileOptions {
        sourceCompatibility = 11
        targetCompatibility = 11
    }
}

