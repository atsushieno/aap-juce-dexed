plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

kotlin {
    jvmToolchain(11)
}

android {
    namespace = "org.androidaudioplugin.ports.juce.dexed"
    compileSdk = libs.versions.android.compileSdk.get().toInteger()

    defaultConfig {
        minSdk = libs.versions.android.minSdk.get().toInteger()
        ndk {
            abiFilters "x86_64", "arm64-v8a"
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID=1",
                        "-DAAP_DIR=${System.env.AAP_DIR != null ? System.env.AAP_DIR : "$projectDir/../external/aap-core"}",
                        "-DAAP_JUCE_DIR=${System.env.AAP_JUCE_DIR != null ? System.env.AAP_JUCE_DIR : "$projectDir/../external/aap-juce"}",
                        "-DANDROID_STL=c++_shared", "-DANDROID_CPP_FEATURES=exceptions rtti"
            }
        }
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    sourceSets {
        main.java.srcDirs +=
                ["../external/dexed/libs/JUCE/modules/juce_gui_basics/native/javaopt/app",
                 "../external/aap-juce/java"]
        main.res.srcDirs +=
                []
    }

    dependencies {
        implementation libs.androidx.core.ktx
        implementation libs.startup.runtime
        implementation libs.aap.core
        implementation libs.aap.midi.device.service
        implementation libs.aap.ui.web
        implementation libs.aap.ui.compose
        implementation libs.aap.ui.compose.app
        // switch to them when you want to debug into aap-core.
        /*
        implementation project(":androidaudioplugin")
        implementation project(":androidaudioplugin-midi-device-service")
        implementation project(":androidaudioplugin-ui-web")
        implementation project(":androidaudioplugin-ui-compose")
        implementation project(":androidaudioplugin-ui-compose-app")
         */
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version libs.versions.cmake.get()
        }
    }

    buildFeatures {
        prefab true
    }

    ndkVersion libs.versions.ndk.get()

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

